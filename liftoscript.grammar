@top Program { expression* }

@precedence {
  unary
  fncall @left
  arrindex @left
  times @left
  plus @left
  cmp @left
  andor @left
  ternary @right
  assign @right
}

expression {
  BinaryExpression |
  NumberExpression |
  WeightExpression | 
  ParenthesisExpression |
  BlockExpression |
  Ternary |
  IfExpression |
  AssignmentExpression |
  IncAssignmentExpression |
  FunctionExpression |
  VariableExpression |
  StateVariable |
  UnaryExpression
}

ParenthesisExpression { "(" expression ")" }
BlockExpression { "{" expression* "}" }
IfExpression {
  @specialize<Keyword, "if"> ParenthesisExpression
  BlockExpression
  (@specialize<Keyword, "else"> @specialize<Keyword, "if"> ParenthesisExpression BlockExpression)*
  (@specialize<Keyword, "else"> BlockExpression)? }
AssignmentExpression { StateVariable !assign "=" expression }
IncAssignmentExpression { StateVariable !assign IncAssignment expression }
VariableExpression { Keyword (!arrindex "[" expression "]")? }
FunctionExpression { Keyword !fncall "(" expression? ("," expression)* ")" }
Ternary { expression !ternary "?" expression ":" expression }
NumberExpression { !unary Plus? Number }
WeightExpression { NumberExpression Unit }

BinaryExpression {
  expression !plus Plus expression |
  expression !times Times expression |
  expression !cmp Cmp expression |
  expression !andor AndOr expression
}

UnaryExpression {
  !unary Not expression
}

@skip { space | LineComment | ";" }

@tokens {
  space { @whitespace+ }
  LineComment { "//" ![\n]* }
  @precedence { LineComment, Times }

  Keyword { @asciiLetter (@asciiLetter | @digit)* }
  StateVariable { "state." Keyword }
  Unit { "lb" | "kg" }
  @precedence { StateVariable, Unit, Keyword }

  Number { (@digit+ ("." @digit+)*) | "." @digit+ | @digit+ "." }
  Plus { ("+" | "-") }
  IncAssignment { ("+=" | "-=" | "*=" | "/=") }
  @precedence { Number, Plus }

  Times { ("*" | "/" | "%") }
  Cmp { ( ">" "="? | "==" | "!=" | "<" "="? ) }
  AndOr { ( "&&" | "||" ) }
  Not { "!" }
}
